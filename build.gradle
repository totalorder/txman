plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.researchgate.release' version '2.7.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url 'https://dl.bintray.com/palantir/releases' // docker-compose-rule is published on bintray
    }
}

dependencies {
    testCompile 'org.postgresql:postgresql:42.2.5'
    testCompile 'com.zaxxer:HikariCP:3.2.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile 'se.deadlock:composed:0.0.1'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('SONATYPE_USERNAME')) {
    signing {
        sign configurations.archives
    }
    
    ext."signing.keyId" = SIGNING_KEY_ID
    ext."signing.secretKeyRingFile" = SIGNING_RING_FILE
    ext."signing.password" = SIGNING_PASSWORD

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                pom.project {
                    name 'TxMan'
                    packaging 'jar'
                    // optionally artifactId can be defined here 
                    description 'Jdbc Postgres connector'
                    url 'https://github.com/totalorder/txman'

                    scm {
                        connection 'git@github.com:totalorder/txman.git'
                        developerConnection 'git@github.com:totalorder/txman.git'
                        url 'https://github.com/totalorder/txman'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/totalorder/txman/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'totalorder'
                            name 'Anton Blomberg'
                            email 'anton@deadlock.se'
                        }
                    }
                }
            }
        }
    }
}